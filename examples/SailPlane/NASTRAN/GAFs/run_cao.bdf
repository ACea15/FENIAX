$EXECUTIVE CONTROL DECK
assign OUTPUT4='./Qhh0_8-20.op4',formatted,UNIT=11
assign OUTPUT4='./Qhj0_8-20.op4',formatted,UNIT=12
assign INPUTT4='../SOL103/Phi20.op4',formatted,UNIT=90
$assign INPUTT4= 'Phi1.op4',formatted,UNIT=91
$NASTRAN NLINES=999999
NASTRAN QUARTICDLM=1
SOL 145
$TIME 10000
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$   K : AERODYNAMIC DOF = 2 * N AERO BOX
$   A : STRUCTURAL DOF  = 6 * N GRID
$   J : N AERO BOX
$   H : N MODES012
$
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$ Retrieve the Spline Matrices form AERO0 and store them in DBALL
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
COMPILE AERO0
ALTER 'CALL.*PLINOUT.*AECASE.*AEBGPDTS.*AEUSETS.*GPGK0.*GDGK0'
EQUIVX GPGK0/SPL_F_AK/ALWAYS
EQUIVX GDGK0/SPL_D_AK/ALWAYS $
CALL DBSTORE SPL_F_AK,,,,//111/112/'DBALL'/0 $
CALL DBSTORE SPL_D_AK,,,,//113/114/'DBALL'/0 $
$
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$ Retrieve the Matrices AJJ D12JK SKJ form PFAERO and store them in DBALL
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
COMPILE PFAERO
ALTER 'AMG.*MKLIST,ACPT/'
TYPE PARM,,I,N,EXIST $
TYPE PARM,,CS,N,CK $
CALL DBFETCH /SPL_F_AK,,,,/111/112/0/0/S,EXIST $
CALL DBFETCH /SPL_D_AK,,,,/113/114/0/0/S,EXIST $
EQUIVX AJJT/A_JJT/ALWAYS $
EQUIVX SKJ/S_KJ/ALWAYS $
EQUIVX D1JK/D1_KJ/ALWAYS $
EQUIVX D2JK/D2_KJ/ALWAYS $
IF ( YESWKK ) THEN $
    EQUIVX WKK/W_KK/ALWAYS $
    MPYAD W_KK,S_KJ,/WS_KJ $
    EQUIVX WS_KJ/S_KJ/ALWAYS $
ENDIF $
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$
$    EVALUATE THE QAA_ MATRIX:
$
$    Q_HH=Phi_HA*SPL_F_AK*S_KJ*INV(A_JJ)*D_JK*SPL_D_KA*Phi_AH
$
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
INPUTT4 /Phi_AH,,,,/1/90 $
$INPUTT4 /Phi_AH1,,,,/1/91 $
TRNSP Phi_AH/Phi_HA $
$TRNSP Phi_AH1/Phi_HA $
TRNSP A_JJT/A_JJ $
TRNSP SPL_D_AK/SPL_D_KA $
DECOMP A_JJ/L_AJJ,U_AJJ,, $
CK = CMPLX(0.,KBAR) $
ADD5 D1_KJ,D2_KJ,,,/D_KJ//CK $
TRNSP D_KJ/D_JK $
MPYAD D_JK,SPL_D_KA,/D_JA $
FBS L_AJJ,U_AJJ,D_JA/Q_JA $
MPYAD S_KJ,Q_JA,/Q_KA $
MPYAD SPL_F_AK,Q_KA,/Q_AA $
MPYAD Phi_HA,Q_AA,/Q_HA $
MPYAD Q_HA,Phi_AH,/Q_HH $
OUTPUT4 Q_HH,,,,//0/11///9 $
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$
$    EVALUATE THE QHJ_ MATRIX:
$
$    Q_HJ=Phi_HA*SPL_F_AK*S_KJ*INV(A_JJ)*D_JK*SPL_D_KA
$
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
PARAML A_JJ//'TRAILER'/1/S,N,COL $
MATGEN ,/ID/1/COL $
FBS L_AJJ,U_AJJ,ID/INVAJJ $
MPYAD S_KJ,INVAJJ,/Q_KJ $
MPYAD SPL_F_AK,Q_KJ,/Q_AJ $
MPYAD Phi_HA,Q_AJ,/Q_HJ $
OUTPUT4 Q_HJ,,,,//0/12///9 $
CEND

$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$ CASE CONTROL									  $
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$

TITLE=Sailplane #                                 
$SPC=  100001                                                            
$MPC=  100001
SPC = 2                  
METHOD = 100
FMETHOD = 9010
$
DISP(PLOT) = ALL
$
RESVEC = YES
MODESELECT (STRUCTURE, LMODES = 20)
$ MODESELECT (STRUCTURE,LFREQ=0.001,HFREQ=15.0) 
$ MODESELECT (FLUID,LFREQ=0.001,HFREQ=15.0)
ECHO=NONE

$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
$ BULK   									  $
$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$

BEGIN BULK
PARAM,BAILOUT,0
PARAM,GRDPNT,0                                                              
PARAM,K6ROT,1.0
PARAM,SNORM,20.0
PARAM,POST,0
$PARAM,MAXRATIO,1.0E07 $Default anyway
$PARAM   AUTOSPC YES
MDLPRM  MLTSPLIN 1 $Aero grids can be defined in multiple splines (dafault 0)
PARAM   WTMASS  1.0  
PARAM   OPPHIB  1
PARAM   OPPHIPA 1

EIGRL,100,,,20
SPC1           1  123456  999079
SPCADD         2       1
INCLUDE ../base.bdf
INCLUDE ../dlm_model.bdf
INCLUDE ./aero_flutter.bdf
